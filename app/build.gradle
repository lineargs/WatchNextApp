apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        config {
            if (rootProject.file('keystore.properties').exists()) {
                def props = new Properties()
                props.load(new FileInputStream(rootProject.file('keystore.properties')))

                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
            }
        }
    }
    compileSdkVersion rootProject.compileSdk
    buildToolsVersion rootProject.buildTools
    defaultConfig {
        applicationId "com.lineargs.watchnext"
        minSdkVersion rootProject.minSdk
        targetSdkVersion rootProject.targetSdk
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        buildConfigField 'String', 'MOVIE_DATABASE_API_KEY', TMDbApiKey
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
}

dependencies {
    /* Support Library developer.android.com/topic/libraries/support-library/revisions.html */
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibrary"
    implementation "com.android.support:support-v4:$rootProject.supportLibrary"
    implementation "com.android.support:design:$rootProject.supportLibrary"
    implementation "com.android.support:preference-v7:$rootProject.supportLibrary"
    implementation "com.android.support:support-vector-drawable:$rootProject.supportLibrary"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibrary"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraintLibrary"
    /* We use this import for Material Preference theme */
    implementation "com.android.support:preference-v14:$rootProject.supportLibrary"
    /* Firebase firebase.google.com/docs/android/setup#available_libraries*/
    implementation "com.google.firebase:firebase-crash:$rootProject.firebaseCrash"
    implementation "com.google.firebase:firebase-core:$rootProject.firebaseCore"
    implementation "com.google.firebase:firebase-messaging:$rootProject.firebaseMessaging"
    /* github.com/firebase/firebase-jobdispatcher-android/blob/master/README.md */
    implementation "com.firebase:firebase-jobdispatcher:$rootProject.firebaseJob"
    /* Crashlytics docs.fabric.io/android/changelog.html */
    implementation("com.crashlytics.sdk.android:crashlytics:${rootProject.crashlytics}@aar") {
        transitive = true
    }
    /* Butterknife github.com/JakeWharton/butterknife/blob/master/CHANGELOG.md */
    implementation "com.jakewharton:butterknife:$rootProject.butterknife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknife"
    /* Retrofit github.com/square/retrofit/blob/master/CHANGELOG.md */
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofit"
    /* Picasso github.com/square/picasso/blob/master/CHANGELOG.md */
    implementation "com.squareup.picasso:picasso:$rootProject.picasso"
    /* Espresso developer.android.com/topic/libraries/testing-support-library/release-notes.html*/
    implementation "com.android.support.test.espresso:espresso-idling-resource:$rootProject.espresso"
    /* Testing only dependencies */
    testImplementation 'junit:junit:4.12'
    androidTestImplementation "com.android.support:support-annotations:$rootProject.supportLibrary"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$rootProject.espresso"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espresso"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$rootProject.espresso"
}

apply plugin: 'com.google.gms.google-services'
